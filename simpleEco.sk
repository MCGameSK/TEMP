#
#Github https://github.com/MCGameSK
#



import:
    org.bukkit.inventory.ItemFlag



options:
    GetBackCost: 50



on load:
    if {menuName} is not set:
        set {menuName} to "&8&l퀵 메뉴"
    
    delete {waitForPrint::*}

    set {manual} to written book
    set {_meta} to {manual}.getItemMeta()
    {_meta}.setTitle("manual")
    {_meta}.setAuthor("server")
    {_meta}.addPage("&3&l&o스타듀밸리 맛 &8&l&o가이드북&r%nl%%nl%%nl%&6&l» &8우클릭으로 물고기판매%nl%%nl%&6&l» &8사망시 랜덤템 귀속%nl%%nl%&6&l» &850원 내고 돌려받자%nl%%nl%%nl%%nl%%nl%%nl%&8&l&o~뒷장에 컨텐츠 설명")
    {_meta}.addPage("&2&l&o[ 낚시 ]&r%nl%%nl%%nl%&6&l» &8등급은 4가지 분류%nl%%nl%&6&l» &8인벤토리를 열고%nl%물고기 우클릭시 판매%nl%%nl%&6&l» &8힌트: 밤")
    {_meta}.addPage("&2&l&o[ 유물 ]&r%nl%%nl%%nl%&6&l» &8광질을 하다보면%nl%정동석이 나옴%nl%%nl%&6&l» &8대장장이에게 가져가면%nl%정동석 분해가능%nl%%nl%&6&l» &8박물관에서%nl%기증할수 있다")
    {_meta}.addPage("&2&l&o[ 강화 ]&r%nl%%nl%%nl%&6&l» &8방사능 광석 등%nl%도구와 재료를 들고%nl%손바꾸기로 강화%nl%%nl%&6&l» &8방사능 도구는%nl%성급함1 붙음")
    {_meta}.addPage("&2&l&o[ 반지 ]&r%nl%%nl%%nl%&6&l» &8들고다니는 신호기임%nl%%nl%&6&l» &8종류별로 다양한%nl%포션효과 지속")

    {manual}.setItemMeta({_meta})

    


on join:
    delete {waitForPrint::%player's uuid%}

    loop {oldbie::*}:
        if player's uuid is loop-value:
            set join message to "&e%player%이(가) 다시 참여했습니다"
            exit
    
    add player's uuid to {oldbie::*}
    add 1000 to player's money
    set join message to "&e%player%이(가) 새로 참여했습니다!"

    wait for 5 seconds
    message " "
    message "&a메뉴를 열 수 있습니다. &e&l웅크리기 + 손바꾸기"
    message " "



on death of player with priority normal:
    if attacker is a player:
        exit

    set {_tempRnd} to random integer between 1 and 10

    if {_tempRnd} <= 2:

        wait for 2 ticks

        message "&a아이템을 잃지 않았습니다."

    else if {_tempRnd} <= 9:
        set {_lostAmount} to random integer between 1 and 4
        set {_lostReal} to 0

        loop {_lostAmount} times:
            set {_lostItem} to random element of all items in player's inventory
            if {_lostItem} is not set:
                exit loop

            add {_lostItem} to {lostItems::%player's uuid%::*}
            remove {_lostItem} from player's inventory
            add 1 to {_lostReal}

        wait for 2 ticks

        if {_lostReal} is 0:
            message "&a잃을 아이템이 없었습니다.."
        else:
            message "&c%{_lostReal}%개의 아이템을 잃었습니다."

    else:
        set {_lostAmount} to random integer between 6 and 10
        set {_lostReal} to 0

        loop {_lostAmount} times:
            set {_lostItem} to random element of all items in player's inventory
            if {_lostItem} is not set:
                exit loop

            add {_lostItem} to {lostItems::%player's uuid%::*}
            remove {_lostItem} from player's inventory
            add 1 to {_lostReal}

        wait for 2 ticks

        if {_lostReal} is 0:
            message "&a잃을 아이템이 없었습니다.."
        else:
            message "&c%{_lostReal}%개의 아이템을 잃었습니다."



every 1 seconds:
    loop all players:
        if {cannotDrinkMilk::%loop-player's uuid%} is set:
            remove 1 from {cannotDrinkMilk::%loop-player's uuid%}
            if {cannotDrinkMilk::%loop-player's uuid%} <= 0:
                delete {cannotDrinkMilk::%loop-player's uuid%}



on inventory click:
    if current inventory of player is {menu::%player's uuid%}:
        cancel event

        if event-inventory is open inventory:

            #설명서출력
            if index of clicked slot is 19:
                open book {manual} to player

            #PvP Execute
            if index of clicked slot is 20:
                execute player command "/pvp"

            #화폐 입출금
            if index of clicked slot is 13:
                #입금
                if click type is left mouse button:
                    if {moneyBase} is set:
                        if GetAmount(player, {moneyBase}) >= 1:
                            remove 1 of {moneyBase} from inventory of player
                            add 100 to player's money
                            message "&6&o+ ₩ 100 입금, [ 잔액: ₩ %GetWon(player's money)% ]"
                            play sound "block.chain.place" with volume 1.4 and pitch 1.2 for player
                            
                            execute player command "/seMenu"
                    else:
                        message "&c&o화폐가 설정되지 않았습니다."

                if click type is left mouse button with shift:
                    if {moneyBaseUpper} is set:
                        if GetAmount(player, {moneyBaseUpper}) >= 1:
                            remove 1 of {moneyBaseUpper} from inventory of player
                            add 1000 to player's money
                            message "&6&o+ ₩ 1,000 입금, [ 잔액: ₩ %GetWon(player's money)% ]"
                            play sound "block.chain.place" with volume 1.4 and pitch 1.2 for player
                            
                            execute player command "/seMenu"
                    else:
                        message "&c&o화폐가 설정되지 않았습니다."

                #출금
                if click type is right mouse button:
                    if {moneyBase} is set:
                        if player's money >= 100:
                            if player has enough space for 1 of {moneyBase}:
                                give player 1 of {moneyBase}
                                remove 100 from player's money
                                message "&c&o- ₩ 100 출금, [ 잔액: ₩ %GetWon(player's money)% ]"
                                play sound "block.chain.step" with volume 1.4 and pitch 1.2 for player

                                execute player command "/seMenu"

                            else:
                                message "&c&o인벤토리 공간이 부족합니다."

                        else:
                            message "&c&o잔액이 부족합니다."

                    else:
                        message "&c&o화폐가 설정되지 않았습니다."

                if click type is right mouse button with shift:
                    if {moneyBaseUpper} is set:
                        if player's money >= 1000:
                            if player has enough space for 1 of {moneyBaseUpper}:
                                give player 1 of {moneyBaseUpper}
                                remove 1000 from player's money
                                message "&c&o- ₩ 1,000 출금, [ 잔액: ₩ %GetWon(player's money)% ]"
                                play sound "block.chain.step" with volume 1.4 and pitch 1.2 for player

                                execute player command "/seMenu"

                            else:
                                message "&c&o인벤토리 공간이 부족합니다."

                        else:
                            message "&c&o잔액이 부족합니다."

                    else:
                        message "&c&o화폐가 설정되지 않았습니다."

                #자산출력
                if click type is drop key:
                    if {waitForPrint::%player's uuid%} is not set:
                        set {waitForPrint::%player's uuid%} to true

                        play sound "block.honey_block.break" with volume 1 and pitch 1 for player
                        broadcast "*%player% &6&o[ 잔액: ₩ %GetWon(player's money)% ]"

                        wait for 2 seconds
                        delete {waitForPrint::%player's uuid%}

            #조합대열기
            if index of clicked slot is 24:
                open crafting table to player

            #엔상
            if index of clicked slot is 25:
                if {ecUnlock::%player's uuid%} is true:
                    open player's ender chest to player
                    play sound "block.ender_chest.open" with volume 0.7 and pitch 1 to player
                else:
                    message "&c&o엔더 상자를 해금해야합니다."
            
            #분실물센터
            if index of clicked slot is 3:
                execute player command "/seLostAndFound"

                play sound "block.note_block.chime" with volume 0.7 and pitch 1.8 for player
                wait for 0.15 seconds
                play sound "block.note_block.chime" with volume 0.7 and pitch 1.8 for player

            #분실물센터
            if index of clicked slot is 5:
                execute player command "/relicBook"



    else if current inventory of player is {lostAndFound::%player's uuid%}:
        cancel event

        if event-inventory is open inventory:

            if index of clicked slot >= 9:

                set {_clickedItem} to ((index of clicked slot) - 8)st element of {lostItems::%player's uuid%::*}
                if {_clickedItem} is not set:
                    exit



                if click type is left mouse button:
                    if player's money >= {@GetBackCost}:
                        remove {@GetBackCost} from player's money
                        message "&a분실물을 찾았습니다 &c[ 잔액: ₩ %GetWon(player's money)% ]"
                        play sound "entity.ender_eye.death" with volume 1 and pitch 1.6 for player
#
                        loop {lostItems::%player's uuid%::*}:
                            if loop-value is {_clickedItem}:
                                message "%loop-value%"
#
                        if player has enough space for {_clickedItem}:
                            give player {_clickedItem}
                            remove {_clickedItem} from {lostItems::%player's uuid%::*}
                        else:
                            drop {_clickedItem} at player
                            remove {_clickedItem} from {lostItems::%player's uuid%::*}
                        
                        execute player command "/seLostAndFound"
                    else:
                        message "&c수수료가 필요합니다!"


                
                if click type is drop key:
                    remove {_clickedItem} from {lostItems::%player's uuid%::*}
                    play sound "entity.iron_golem.damage" with volume 0.6 and pitch 0.6 for player
                    execute player command "/seLostAndFound"





on swap hand items with priority normal:
    if player is sneaking:
        cancel event
        execute player command "/seMenu"



command /seMenu:
    trigger:
        set {menu::%player's uuid%} to chest inventory with 3 rows named {menuName}
        
        set slot 19 of {menu::%player's uuid%} to map named "&e&l서버 설명서"

        set {_woodenSword} to wooden sword named "&ePvP" with lore "&7&oPvP 허용/비허용"
        set {_meta} to {_woodenSword}.getItemMeta()
        {_meta}.addItemFlags([ItemFlag.HIDE_ATTRIBUTES])
        {_woodenSword}.setItemMeta({_meta})
        set slot 20 of {menu::%player's uuid%} to {_woodenSword}

        if {moneyBase} is set:
            set slot 13 of {menu::%player's uuid%} to {moneyBase} named "&e&l잔액: &r&e₩ %GetWon(player's money)%" with lore "&a좌클 입금 &e/ &c우클 출금" and " " and "&6Shift &7to x10" and "&6Drop key &7to show off"
        else:
            set slot 13 of {menu::%player's uuid%} to barrier named "&c&l화폐가 설정되지 않음" with lore "&7&o화폐를 설정하세요"
        set slot 24 of {menu::%player's uuid%} to crafting table named "&e&l제작대 열기"

        if {ecUnlock::%player's uuid%} is true:
            set slot 25 of {menu::%player's uuid%} to ("WildsEX" parsed as offline player)'s skull named "&e&l엔더 상자 열기"
        else:
            set slot 25 of {menu::%player's uuid%} to ("WildsEX" parsed as offline player)'s skull named "&e&l엔더 상자 열기" with lore "&4&o잠겨있습니다"
        set slot 3 of {menu::%player's uuid%} to writable book named "&e&l분실물 센터" with lore "&7&o잃어버린 물건이 있으신가요?"
        set slot 5 of {menu::%player's uuid%} to book named "&e&l유물 도감" with lore "&7&o기증한 유물이 등록됩니다"



        open {menu::%player's uuid%} to player



command /seLostAndFound:
    trigger:
        set {lostAndFound::%player's uuid%} to chest inventory with 6 rows named "&4&l분실물 센터"
        
        loop 9 times:
            set slot (loop-number - 1) of {lostAndFound::%player's uuid%} to gray stained glass pane named " "
        set slot 4 of {lostAndFound::%player's uuid%} to writable book named "&e&l안전하게 맡아두었습니다!" with lore " " and "&c&l돌려받으려면 수수료가 필요합니다" and "&6&o₩ %GetWon({@GetBackCost})%"

        set {_loopTimes} to 0
        loop {lostItems::%player's uuid%::*}:
            if name of loop-value is set:
                set slot (9 + {_loopTimes}) of {lostAndFound::%player's uuid%} to paper named "&a%name of loop-value%" with lore "&e수량 : &e&o%item amount of loop-value%" and " " and "&6Click &a&l돌려받기" and "&6Drop key &c&l삭제"
            else:
                set slot (9 + {_loopTimes}) of {lostAndFound::%player's uuid%} to paper named "&a%type of loop-value%" with lore "&e수량 : &e&o%item amount of loop-value%" and " " and "&6Click &a&l돌려받기" and "&6Drop key &c&l삭제"
            add 1 to {_loopTimes}



        open {lostAndFound::%player's uuid%} to player



command /seSetMenuName [<text>]:
    trigger:
        if player is op:
            if arg 1 is not set:
                message "&c&o설정할 메뉴 이름을 입력하지 않았습니다."
                exit

            set {menuName} to coloured arg 1
            message "&a&l메뉴 이름을 다음으로 설정합니다."
            message {menuName}
        else:
            message "&c&oOP required"



command /seResetMenuName:
    trigger:
        if player is op:
            set {menuName} to "&8&l퀵 메뉴"
            message "&a&l메뉴 이름을 다음으로 설정합니다."
            message {menuName}
        else:
            message "&c&oOP required"



command /seSetBase:
    trigger:
        if player is op:
            if player's held item is 0 air:
                delete {moneyBase}
                message "&c&l화폐 단위를 삭제했습니다."
            else:
                set {moneyBase} to 1 of player's held item
                message "&a&l화폐 단위를 설정합니다. &r[%{moneyBase}%]"
        else:
            message "&c&oOP required"



command /seSetBaseUpper:
    trigger:
        if player is op:
            if player's held item is 0 air:
                delete {moneyBaseUpper}
                message "&c&l큰 화폐 단위를 삭제했습니다."
            else:
                set {moneyBaseUpper} to 1 of player's held item
                message "&a&l큰 화폐 단위를 설정합니다. &r[%{moneyBaseUpper}%]"
        else:
            message "&c&oOP required"



command /seResetOldbie [<integer>]:
    trigger:
        if player is op:
            if arg 1 is not set:
                message "&c&o접속했던 플레이어 리스트를 삭제하려면 &e&l/seResetOldbie 1234"
                exit
            
            if arg 1 is 1234:
                delete {oldbie::*}
                message "&c&l접속했던 모든 플레이어 리스트를 삭제했습니다."
        else:
            message "&c&oOP required"



command /sePrintAccount [<player>]:
    trigger:
        if player is op:
            if arg 1 is set:
                message "&7&o%arg 1%: %GetWon(player's money)%"
                exit

            loop all players:
                message "&7&o%loop-player%: %GetWon(loop-player's money)%"
            
        else:
            message "&c&oOP required"



command /seSetAccount [<player>] [<integer>]:
    trigger:
        if player is op:
            if arg 2 is set:
                set arg 1's money to arg 2
                message "&f%arg 1%&e의 돈을 &f%GetWon(arg 2)%&e(으)로 수정했습니다."
            
        else:
            message "&c&oOP required"



command /seEcUnlock [<player>]:
    trigger:
        if command sender is console or op:
            set {ecUnlock::%arg 1's uuid%} to true
        else:
            message "&c&oOP required"



command /seReset [<text>]:
    trigger:
        if player is op:
            if arg 1 is "confirm":
                message "&c에센셜 머니, 분실물 정보, 엔더 상자 해금 정보를 삭제합니다."
                delete {lostItems::*}
                delete {ecUnlock::*}
                loop all offline players:
                    set loop-value's money to 0
            else:
                message "&cYou have to confirm it"
        else:
            message "&c&oOP required"
