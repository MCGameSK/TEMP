import:
    org.bukkit.inventory.ItemFlag
    org.bukkit.Bukkit
    org.bukkit.boss.BarColor
    org.bukkit.boss.BarStyle
    org.bukkit.boss.BarFlag



options:
    EnderTridentTag: 860201
    FireworkChargeTag: 860202
    PortableEnderChestTag: 860203
    PotionHealthTag: 860204
    PotionArmorTag: 860205
    PotionKnockbackTag: 860206
    RainbowPaintTag: 860207
    CowShearsTag: 860208
    JumpBootsTag: 860209



on load:
    set {enderTrident} to trident of loyalty 3 named "&9&l엔더창" with lore "&e&o던진곳으로 TP"
    set custom model data of {enderTrident} to {@EnderTridentTag}

    set {fireworkCharge} to firework star of unbreaking named "&c&l충전식 폭죽" with lore "&8&o좌클릭시 화약을 소모하여 충전" and " " and "&e&l우클릭 &e&o- 기본 출력" and "&e&l손바꾸기 &e&o- 가속"
    set custom model data of {fireworkCharge} to {@FireworkChargeTag}
    set {_meta} to {fireworkCharge}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_ENCHANTS])
    {fireworkCharge}.setItemMeta({_meta})

    set {portableEnderChest} to clock of unbreaking named "&d&l엔더 상자 해금" with lore "&e&o우클릭으로 해금"
    set custom model data of {portableEnderChest} to {@PortableEnderChestTag}
    set {_meta} to {portableEnderChest}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_ENCHANTS])
    {portableEnderChest}.setItemMeta({_meta})

    set {potionHealth} to potion of healing named "&6&l농축된 재생 포션" with lore "&9최대 체력 1칸 영구 증가"
    set custom model data of {potionHealth} to {@PotionHealthTag}
    set {_meta} to {potionHealth}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_POTION_EFFECTS])
    {potionHealth}.setItemMeta({_meta})

    set {potionArmor} to potion of fire resistance named "&6&l농축된 저항 포션" with lore "&9방어 1칸 영구 증가"
    set custom model data of {potionArmor} to {@PotionArmorTag}
    set {_meta} to {potionArmor}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_POTION_EFFECTS])
    {potionArmor}.setItemMeta({_meta})

    set {potionKnockback} to potion of speed named "&6&l증류된 저항 포션" with lore "&9넉백 저항 영구 증가"
    set custom model data of {potionKnockback} to {@PotionKnockbackTag}
    set {_meta} to {potionKnockback}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_POTION_EFFECTS])
    {potionKnockback}.setItemMeta({_meta})

    set {rainbowColors::*} to red, orange, yellow, green, blue, indigo and purple

    set {rainbowPaint} to dragon breath named "&c&l무&e&l지&a&l개&9&l빛 &b&l물감" with lore "&e&o우클릭으로 착용한 갑옷 염색"
    set custom model data of {rainbowPaint} to {@RainbowPaintTag}

    set {cowShears} to shears named "&4&l가위" with lore "&4&o소의 가죽을 벗길수 있다"
    set custom model data of {cowShears} to {@CowShearsTag}
    set {_meta} to {cowShears}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_POTION_EFFECTS])
    {cowShears}.setItemMeta({_meta})

    set {jumpBoots} to unbreakable leather boots of feather falling 5 named "&b&l점프 슈즈" with lore "&e&o웅크리기로 차징"
    set custom model data of {jumpBoots} to {@JumpBootsTag}

    delete {jumpBar::*}
    delete {jumpBarProgress::*}



on join:
    delete {fireworkWait::%player's uuid%}
    
    if {lastHealth::%player's uuid%} is set:
        if {lastHealth::%player's uuid%} > 10:
            set health of player to {lastHealth::%player's uuid%}



on quit:
    set {lastHealth::%player's uuid%} to health of player



on sneak toggle:
    if player is not sneaking:
        if player is on ground:
            if custom model data of player's boots is {@JumpBootsTag}:
                if {jumpBar::%player's uuid%} is set:
                    {jumpBar::%player's uuid%}.setVisible(false)

                set {jumpBar::%player's uuid%} to Bukkit.createBossBar("&5&lJump Charging", BarColor.valueOf("PINK"), BarStyle.valueOf("SOLID"), [BarFlag.valueOf("PLAY_BOSS_MUSIC")])
                {jumpBar::%player's uuid%}.addPlayer(player)

                set {jumpBarProgress::%player's uuid%} to 0
                {jumpBar::%player's uuid%}.setProgress({jumpBarProgress::%player's uuid%} / 100)

                {jumpBar::%player's uuid%}.setVisible(true)
    else:
        {jumpBar::%player's uuid%}.setVisible(false)



every 0.2 seconds:
    loop all players:
        if loop-player is sneaking:
            if custom model data of loop-player's boots is {@JumpBootsTag}:
                if loop-player is on ground:
                    if {jumpBar::%loop-player's uuid%}.isVisible() is true:
                        if {jumpBarProgress::%loop-player's uuid%} < 100:
                            add 10 to {jumpBarProgress::%loop-player's uuid%}
                            {jumpBar::%loop-player's uuid%}.setProgress({jumpBarProgress::%loop-player's uuid%} / 100)
                        play sound "block.note_block.cow_bell" with volume 0.5 and pitch 0.8 for loop-player


                        
        if {jumpTrail::%loop-player's uuid%} is set:

            if {jumpExplode::%loop-player's uuid%} is true:
                play sound "entity.firework_rocket.large_blast" with volume 0.8 at loop-player
                show flash at loop-player

            loop 4 times:
                set {_trailLoc} to location of loop-player
                add random number between -0.3 and 0.3 to x-coord of {_trailLoc}
                add random number between -0.5 and 0.5 to y-coord of {_trailLoc}
                add random number between -0.3 and 0.3 to z-coord of {_trailLoc}
                show firework's spark at {_trailLoc}

            remove 1 from {jumpTrail::%loop-player's uuid%}
            if {jumpTrail::%loop-player's uuid%} < 0:
                delete {jumpTrail::%loop-player's uuid%}
                delete {jumpExplode::%loop-player's uuid%}



on jump:
    if player is sneaking:
        if custom model data of player's boots is {@JumpBootsTag}:
            if {jumpBar::%player's uuid%}.isVisible():
                if {jumpBarProgress::%player's uuid%} is not 0:
                    if {jumpBarProgress::%player's uuid%} is 100:
                        wait for 1 ticks
                        push player upward at speed {jumpBarProgress::%player's uuid%} / 40
                        set {jumpBarProgress::%player's uuid%} to 0
                        {jumpBar::%player's uuid%}.setProgress({jumpBarProgress::%player's uuid%} / 100)
                        play sound "entity.generic.explode" with volume 0.8 and pitch 1.8 at player
                        show large explosion at player

                        set {jumpTrail::%player's uuid%} to 5
                        set {jumpExplode::%player's uuid%} to true

                        loop 50 times:
                            set {_effLoc} to location of player
                            add random number between -4 and 4 to x-coord of {_effLoc}
                            add random number between -4 and 4 to z-coord of {_effLoc}
                            show cloud at {_effLoc}
                    else:
                        wait for 1 ticks
                        push player upward at speed {jumpBarProgress::%player's uuid%} / 80
                        set {jumpBarProgress::%player's uuid%} to 0
                        {jumpBar::%player's uuid%}.setProgress({jumpBarProgress::%player's uuid%} / 100)
                        play sound "entity.generic.explode" with volume 0.5 and pitch 1.8 at player
                        show large explosion at player

                        set {jumpTrail::%player's uuid%} to 3

                        loop 12 times:
                            set {_effLoc} to location of player
                            add random number between -1.2 and 1.2 to x-coord of {_effLoc}
                            add random number between -1.2 and 1.2 to z-coord of {_effLoc}
                            show cloud at {_effLoc}



on left click:
    if custom model data of 1 of event-item is {@FireworkChargeTag}:
        if GetAmount(player, gunpowder) >= 1:
            remove 1 of gunpowder from inventory of player
            add 3 to {fireworks::%player's uuid%}
            send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player
        else:
            message "&c&l인벤토리에 화약이 없습니다."



on swap hand items with priority highest:
    if custom model data of 1 of player's tool is {@FireworkChargeTag}:
        cancel event

        if gliding state of player is true:
            if {fireworkWait::%player's uuid%} is not set:
                if {fireworks::%player's uuid%} >= 1:
                    set {fireworkWait::%player's uuid%} to true

                    remove 1 from {fireworks::%player's uuid%}
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player

                    play sound "entity.firework_rocket.launch" with volume 1 and pitch 1 to player
                    push player forward at speed 1.6

                    wait for 0.4 seconds
                    delete {fireworkWait::%player's uuid%}
                else:
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player

    if custom model data of 1 of player's offhand tool is {@FireworkChargeTag}:
        cancel event

        if gliding state of player is true:
            if {fireworkWait::%player's uuid%} is not set:
                if {fireworks::%player's uuid%} >= 1:
                    set {fireworkWait::%player's uuid%} to true

                    remove 1 from {fireworks::%player's uuid%}
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player

                    play sound "entity.firework_rocket.launch" with volume 1 and pitch 1 to player
                    push player forward at speed 1.6

                    wait for 0.4 seconds
                    delete {fireworkWait::%player's uuid%}
                else:
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player



on right click:
    if custom model data of 1 of event-item is {@FireworkChargeTag}:
        cancel event

        if gliding state of player is true:
            if {fireworkWait::%player's uuid%} is not set:
                if {fireworks::%player's uuid%} >= 1:
                    set {fireworkWait::%player's uuid%} to true

                    remove 1 from {fireworks::%player's uuid%}
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player

                    shoot firework

                    wait for 0.4 seconds
                    delete {fireworkWait::%player's uuid%}
                else:
                    send action bar "&c&l남은 폭죽 : &e&o%{fireworks::%player's uuid%}%" to player
    


    if custom model data of 1 of player's tool is {@PortableEnderChestTag}:
        cancel event

        execute console command "/seEcUnlock %player%"
        message "&a&l메뉴의 엔더 상자가 해금되었습니다."
        play sound "entity.firework_rocket.launch" with volume 1 for player

        remove 1 of player's tool from inventory of player



    if custom model data of 1 of event-item is {@RainbowPaintTag}:
        cancel event

        add 1 to {rainbowIndex::%player's uuid%}
        if {rainbowIndex::%player's uuid%} > 7:
            set {rainbowIndex::%player's uuid%} to 1

        if {rainbowIndex::%player's uuid%} is 1:
            send action bar "&c&lRED" to player
        else if {rainbowIndex::%player's uuid%} is 2:
            send action bar "&6&lORANGE" to player
        else if {rainbowIndex::%player's uuid%} is 3:
            send action bar "&e&lYELLOW" to player
        else if {rainbowIndex::%player's uuid%} is 4:
            send action bar "&2&lGREEN" to player
        else if {rainbowIndex::%player's uuid%} is 5:
            send action bar "&1&lBLUE" to player
        else if {rainbowIndex::%player's uuid%} is 6:
            send action bar "&4&lINDIGO" to player
        else if {rainbowIndex::%player's uuid%} is 7:
            send action bar "&5&lPURPLE" to player
        play sound "item.bottle.fill" with volume 0.7 for player

        if player's helmet is leather helmet:
            dye player's helmet {rainbowIndex::%player's uuid%}st element of {rainbowColors::*}

        if player's chestplate is leather chestplate:
            dye player's chestplate {rainbowIndex::%player's uuid%}st element of {rainbowColors::*}

        if player's leggings is leather leggings:
            dye player's leggings {rainbowIndex::%player's uuid%}st element of {rainbowColors::*}

        if player's boots is leather boots:
            dye player's boots {rainbowIndex::%player's uuid%}st element of {rainbowColors::*}

    

on right click on a cow with priority normal:
    if custom model data of 1 of event-item is {@CowShearsTag}:
        play sound "entity.sheep.shear" with volume 1.4 at clicked entity's head
        play sweep attack at clicked entity's head

        set {_effLoc} to location of clicked entity
        
        if clicked entity.isAdult() is false:
            kill clicked entity
            loop 10 times:
                add random number between -0.2 and 0.2 to x-coord of {_effLoc}
                add random number between 0 and 0.2 to y-coord of {_effLoc}
                add random number between -0.2 and 0.2 to z-coord of {_effLoc}
                play red coloured dust at {_effLoc}
        else:
            drop random integer between 1 and 2 of leather at clicked entity
            clicked entity.setBaby()
            loop 10 times:
                add random number between -0.4 and 0.4 to x-coord of {_effLoc}
                add random number between 0 and 0.3 to y-coord of {_effLoc}
                add random number between -0.4 and 0.4 to z-coord of {_effLoc}
                play red coloured dust at {_effLoc}
        


on projectile hit:
    if custom model data of item of event-projectile is {@EnderTridentTag}:

        set {_loc} to event-location

        loop all blocks between {_loc} and shooter:
            loop all blocks in radius 1 around loop-block:
                show cloud at loop-block-2

        set {_loc}'s pitch to pitch of shooter
        set {_loc}'s yaw to yaw of shooter
        teleport shooter to {_loc}

        play sound "item.chorus_fruit.teleport" with volume 1 and pitch 1 at event-location
        


on consume:
    if custom model data of event-item is {@PotionHealthTag}:
        if generic max health attribute of player >= 40:
            cancel event
            message "&d&l이미 최대 체력입니다."
            exit
        
        set generic max health attribute of player to generic max health attribute of player + 2
        play sound "item.totem.use" with volume 1 for player
        play sound "ui.toast.challenge_complete" with volume 1 for player
        send title "&d&l최대 체력 증가" with subtitle "&7&o영구적으로 최대 체력이 증가합니다" to player for 4 seconds
        apply glowing to player for 4 seconds

        if generic max health attribute of player >= 40:
            message " "
            message "&c&l최대 체력에 도달했습니다. 더 이상 해당 포션은 사용할수 없습니다."
            message " "

    if custom model data of event-item is {@PotionArmorTag}:
        if generic armor attribute of player >= 20:
            cancel event
            message "&d&l이미 최대 방어력입니다."
            exit

        set generic armor attribute of player to generic armor attribute of player + 2
        play sound "item.totem.use" with volume 1 for player
        play sound "ui.toast.challenge_complete" with volume 1 for player
        send title "&d&l기본 방어 증가" with subtitle "&7&o영구적으로 방어가 증가합니다" to player for 4 seconds
        apply glowing to player for 4 seconds

        if generic armor attribute of player >= 20:
            message " "
            message "&c&l최대 방어력에 도달했습니다. 더 이상 해당 포션은 사용할수 없습니다."
            message " "

    if custom model data of event-item is {@PotionKnockbackTag}:
        if generic knockback resistance attribute of player >= 0.4:
            cancel event
            message "&d&l이미 최대 넉백 저항입니다."
            exit

        set generic knockback resistance attribute of player to generic knockback resistance attribute of player + 0.04
        play sound "item.totem.use" with volume 1 for player
        play sound "ui.toast.challenge_complete" with volume 1 for player
        send title "&d&l넉백 저항 증가" with subtitle "&7&o영구적으로 넉백 저항이 증가합니다" to player for 4 seconds
        apply glowing to player for 4 seconds

        if generic knockback resistance attribute of player >= 0.4:
            message " "
            message "&c&l최대 넉백 저항에 도달했습니다. 더 이상 해당 포션은 사용할수 없습니다."
            message " "



command /fiDropAllItems:
    trigger:
        if player is op:
            drop {enderTrident}
            drop {fireworkCharge}
            drop {portableEnderChest}
            drop {potionHealth}
            drop {potionArmor}
            drop {potionKnockback}
            drop {rainbowPaint}
            drop {cowShears}
            drop {jumpBoots}
            message "&a&l모든 커스텀 아이템을 드랍합니다."
        else:
            message "&c&oOP required"



command /fiStatus:
    trigger:
        message "&c&l최대 체력: &e&o%generic max health attribute of player% &7&o/ 40"
        message "&c&l방어: &e&o%generic armor attribute of player% &7&o/ 20"
        message "&c&l넉백 저항: &e&o%generic knockback resistance attribute of player% &7&o/ 0.4"



command /fiPrintAmountFireworks:
    trigger:
        if player is op:
            loop all players:
                message "&7&o%loop-player% has %{fireworks::%loop-player's uuid%}%"
        else:
            message "&c&oOP required"
